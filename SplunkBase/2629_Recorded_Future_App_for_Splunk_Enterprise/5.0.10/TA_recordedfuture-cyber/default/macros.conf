[format_evidence]
definition = spath input=EvidenceDetails path="EvidenceDetails{}." output=tmpED \
      | mvexpand tmpED \
      | spath input=tmpED \
      | fields - tmpED \
      | eval Evidence='Criticality'.":".'Rule'.": ".'EvidenceString' \
      | sort -Criticality \
      | fields - Rule EvidenceString Criticality CriticalityLabel Timestamp \
      | mvcombine delim="\n" Evidence
iseval = 0

[rf_hits(1)]
args = infield
definition = dedup $infield$ \
      | lookup rf_ip_threatfeed Name as $infield$ OUTPUT Name as RF_Hit, Risk, RiskString, EvidenceDetails \
      | search RF_Hit=* \
      | eval Rule = spath(EvidenceDetails,"EvidenceDetails{}.Rule") \
      | eval EvidenceString = spath(EvidenceDetails,"EvidenceDetails{}.EvidenceString")
iseval = 0

[unpack_metrics]
definition = eval metricsData=spath(metrics,"data{}*") \
      | mvexpand metricsData \
      | eval xx=spath(metricsData,"type") \
      | eval yy=spath(metricsData, "value") \
      | eval metricJson="\"".xx."\": ".yy\
      | fields - xx yy metricsData \
      | mvcombine metricJson \
      | eval mj="{\"metrics\": {".mvjoin(metricJson,",")."}}" \
      | spath input=mj \
      | fields - metricJson mj
iseval = 0

[unpack_relatedEntities(1)]
args = relatedCategory
definition = eval tmp=spath(relatedEntities, "data{}") \
      | eval relCatIndex=mvfind(tmp, "\"$relatedCategory$\"") \
      | eval relCat=mvindex(tmp,relCatIndex) \
      | fields relCat \
      | eval category=spath(relCat,"type") \
      | spath input=relCat path="entities{}*" output=relCatEnts \
      | mvexpand relCatEnts \
      | fields - relCat \
      | eval count=spath(relCatEnts, "count") \
      | eval name=spath(relCatEnts, "entity.name") \
      | eval id=spath(relCatEnts, "entity.id")\
      | eval type=spath(relCatEnts,"entity.type") \
      | fields - relCatEnts
iseval = 0

[unpack_riskyCIDRIPs]
definition = fields riskyCIDRIPs \
      | spath input=riskyCIDRIPs path="data{}*" output=riskyC \
      | fields - riskyCIDRIPs \
      | mvexpand riskyC \
      | spath input=riskyC \
      | fields - riskyC \
      | eval IP='ip.name' \
      | eval Score=round(score,0) \
      | sort -Score, name
iseval = 0

[to_date(1)]
args = dateString
definition = strftime(strptime($dateString$, "%Y-%m-%dT%H:%M:%S.%NZ"), "%b %d, %Y")
iseval = 0

[to_splunk_time(1)]
args = dateString
definition = strptime($dateString$, "%Y-%m-%dT%H:%M:%S.%NZ")
iseval = 0

[to_time(1)]
args = dateString
definition = strftime(strptime($dateString$, "%Y-%m-%dT%H:%M:%S.%NZ"), "%b %d, %Y %X")
iseval = 0

[get_updated_time(1)]
args = risklist
definition = | inputlookup checkpoints | eval key=_key | search key=$risklist$ \
             | eval Time=strftime(updated,"%a %b %d, %Y at %I:%M:%S:%p")
iseval = 0